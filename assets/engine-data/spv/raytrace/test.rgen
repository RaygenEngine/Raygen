#version 460
#extension GL_GOOGLE_include_directive : enable
#extension GL_EXT_ray_tracing : require
#include "global.glsl"
#include "rt-global.glsl"

#include "random.glsl"
#include "sampling.glsl"
#include "fragment.glsl"
#include "bsdf.glsl"
#include "attachments.glsl"
#include "onb.glsl"
#include "sky.glsl"

layout(set = 1, binding = 0) uniform UBO_Camera {   
	vec3 position;
	float pad0;
	mat4 view;
	mat4 proj;
	mat4 viewProj;
	mat4 viewInv;
	mat4 projInv;
	mat4 viewProjInv; 
} cam;

layout(set = 2, binding = 0, rgba32f) uniform image2D image;
layout(set = 2, binding = 1, rgba32f) uniform image2D progressiveResult;
layout(set = 2, binding = 2, rgba32f) uniform image2D momentsBuffer;

layout(location = 0) rayPayloadEXT hitPayload prd;


#include "raytrace/rt-indirect.glsl"

void WriteSky(); 

struct ReprojectionData {
	vec3 color;
	vec2 moments;
	float historyLength; // 0 means reprojection was invalid
};

ReprojectionData GetReprojection(vec2 uv);
 
void main() {

	vec2 uv = (vec2(gl_LaunchIDEXT.xy) + vec2(0.5)) / vec2(gl_LaunchSizeEXT.xy);
	float fragDepth = texture(g_DepthSampler, uv).r;
	if(fragDepth >= 1.)
	{
		// sky
		WriteSky();
		return; 
	}

	if(frame >= convergeUntilFrame && convergeUntilFrame != 0) {
		vec3 indirect = imageLoad(progressiveResult, ivec2(gl_LaunchIDEXT.xy)).xyz;
		imageStore(image, ivec2(gl_LaunchIDEXT.xy), vec4(indirect, 1.));
		return;
	}

	Fragment frag = getFragmentFromGBuffer(
		fragDepth,
		cam.viewProjInv,
		g_NormalSampler,
		g_ColorSampler,
		g_SpecularSampler,
		g_EmissiveSampler,
		uv);

	// Extract BrdfInfo
	FragBrdfInfo brdfInfo;
	brdfInfo.diffuseColor = frag.diffuseColor;
	brdfInfo.f0 = frag.f0;
	brdfInfo.a = frag.a;

	// Extract SpaceInfo
	FsSpaceInfo fragSpace = GetFragSpace_World(frag.normal, frag.position, cam.position);

	
	ReprojectionData reproj = GetReprojection(uv);
	float historyLength = reproj.historyLength;

	vec3 accumColor = vec3(0);
	uint actualSamples = 1;

	float historyCalcualted = 1.;
	if (historyLength < 2) {
	   actualSamples = 8;
	   historyCalcualted = 8.;
	}
	
	
	actualSamples *= samples;
	for(uint smpl = 0; smpl < actualSamples && depth > 0; ++smpl){
		// NOTE: Don't forget to initialize the ray here. 
		prd.seed = tea16(gl_LaunchIDEXT.y * gl_LaunchSizeEXT.x + gl_LaunchIDEXT.x, frame * samples + smpl); // PERF:
		prd.depth = 0;
		prd.accumThroughput = vec3(1.f);
		accumColor += TraceIndirect(fragSpace, brdfInfo);
	}
	vec3 curColor = (accumColor / actualSamples);

	vec2 moments;
	moments.r = luminance(curColor);
	moments.g = moments.r * moments.r;

	// Mix reprojection
	{
		#define minColorAlpha 0.05
		#define minMomentsAlpha 0.05

		const float colorAlpha = max(1. / (historyLength + 1.), minColorAlpha);
		const float momentsAlpha = max(1. / (historyLength + 1.), minMomentsAlpha);

		moments = mix(reproj.moments, moments, momentsAlpha);
		curColor = mix(reproj.color, curColor, colorAlpha);
	}

	// from SVGF paper:
	float variance = max(0., moments.g - moments.r * moments.r);

	variance *= 16.f / max(historyLength + historyCalcualted, 32.f);

	if (historyLength < 10) {
		variance += 0.2;

	}

	imageStore(progressiveResult, ivec2(gl_LaunchIDEXT.xy), vec4(curColor, variance));
	imageStore(image, ivec2(gl_LaunchIDEXT.xy), vec4(curColor, variance));

	imageStore(momentsBuffer, ivec2(gl_LaunchIDEXT.xy), vec4(moments, fragDepth, historyLength + historyCalcualted));
}

void WriteSky() {
	const vec2 pixelCenter = vec2(gl_LaunchIDEXT.xy) + vec2(0.5);
	const vec2 inUV        = pixelCenter / vec2(gl_LaunchSizeEXT.xy);
	vec2       d           = inUV * 2.0 - 1.0;

	vec4 origin    = cam.viewInv * vec4(0, 0, 0, 1);
	vec4 target    = cam.projInv * vec4(d.x, d.y, 1, 1);
	vec4 direction = cam.viewInv * vec4(normalize(target.xyz), 0);        
	
	vec3 color = GetSkyColor(origin.xyz, direction.xyz);
	
	imageStore(image, ivec2(gl_LaunchIDEXT.xy), vec4(color, 1.f));
}

//
//  PRE PROCESS (reproject)
//

bool IsReprojValid(ivec2 coord, float expectedPrevDepth, vec4 centerUvDrawIndex, out vec4 momentsSample)
{
	if (coord.x < 0 || coord.y < 0 || coord.x >= gl_LaunchSizeEXT.x || coord.y > gl_LaunchSizeEXT.y) {
		return false;
	}

	momentsSample = imageLoad(momentsBuffer, coord);
	if (abs(expectedPrevDepth - momentsSample.z) > 5e-3) {
		return false;
	}

	

	vec4 thisUvDrawIndex = texelFetch(g_UVDrawIndexSampler, coord, 0);
	if (abs(thisUvDrawIndex.z - centerUvDrawIndex.z) > 0.5) {
		return false;
	}

	return true;
}


ReprojectionData ReprojSample(vec2 sampleUv, vec4 centerUvDrawIndex, float expectedPrevDepth)
{
	ivec2 pixelPos = ivec2(floor(sampleUv));
	ivec2 offset[4] = { ivec2(0, 0), ivec2(1, 0), ivec2(0, 1), ivec2(1, 1) };

	float x = fract(sampleUv.x);
	float y = fract(sampleUv.y);

	float w[4] = { 
		(1 - x) * (1 - y),
		      x * (1 - y),
		(1 - x) * y,
		      x * y 
	};

	vec4 color = vec4(0);
	vec4 moments = vec4(0);
	float sumW = 0;
	
	for (int sampleIdx = 0; sampleIdx < 4; sampleIdx++) {
		ivec2 loc = pixelPos + offset[sampleIdx];
		vec4 momentsSample;
		if (IsReprojValid(loc, expectedPrevDepth, centerUvDrawIndex, momentsSample)) {
			color += w[sampleIdx] * imageLoad(progressiveResult, loc);
			moments += w[sampleIdx] * momentsSample;
			sumW += w[sampleIdx];
		}
	}

	// TODO: test history accumulation per sample in conjunction with adaptive sampling
	// This "valid" check should probably have min bias
	if (sumW > 0) {
		color /= sumW;
		moments /= sumW;
	}

	ReprojectionData reproj;
	reproj.color = color.rgb;
	reproj.moments = moments.rg;

	reproj.historyLength = moments.a;
	return reproj;
}

struct ReprojectionData2 {
	vec3 color;
	vec2 moments;
	float historyLength; // 0 means reprojection was invalid
};


ReprojectionData GetReprojection(vec2 uv) {
	// PERF:
	if (frame < 1) {
		ReprojectionData repr;
		repr.color = vec3(0);
		repr.moments = vec2(0);
		repr.historyLength = 0;
		return repr;
	}

	// xy: ScreenSpace (uv) motion vector
	// z: expected previous depth
	const vec4 motionSample = texture(g_VelocitySampler, uv);

	const vec2 motion = motionSample.xy;
	const float expectedDepth = motionSample.z;

	const vec2 posPrev = vec2(gl_LaunchIDEXT.xy) - vec2(motion * gl_LaunchSizeEXT.xy);


	const vec4 centerUvDrawIndex = texelFetch(g_UVDrawIndexSampler, ivec2(gl_LaunchIDEXT.xy), 0);

	return ReprojSample(posPrev, centerUvDrawIndex, expectedDepth);
}








