#version 460
#extension GL_GOOGLE_include_directive : enable
#extension GL_EXT_ray_tracing : require
#extension GL_EXT_scalar_block_layout : enable
#extension GL_EXT_nonuniform_qualifier : enable
#extension GL_EXT_buffer_reference2 : enable

// TODO:
#define RAY

#include "global.glsl"
#include "raytrace/rtspec/rtspec.glsl"

#include "attachments.glsl"
#include "bsdf.glsl"
#include "onb.glsl"
#include "surface.glsl"

layout(set = 1, binding = 0) uniform UBO_Camera { Camera cam; };
layout(set = 2, binding = 0, rgba32f) uniform image2D image;
layout(set = 2, binding = 1, rgba32f) uniform image2D progressiveResult;
layout(set = 2, binding = 2, rgba32f) uniform image2D momentsBuffer;
layout(set = 3, binding = 0) uniform accelerationStructureEXT topLevelAs;
layout(set = 4, binding = 1) uniform sampler2D textureSamplers[];

layout(location = 0) rayPayloadEXT hitPayload prd;

struct ReprojectionData {
	vec3 color;
	vec2 moments;
	float historyLength; // 0 means reprojection was invalid
};

ReprojectionData GetReprojection(vec2 uv);

vec3 RadianceOfRay(vec3 nextOrigin, vec3 nextDirection) {
	prd.radiance = vec3(0);
	prd.depth += 1;

    uint  rayFlags = gl_RayFlagsOpaqueEXT | gl_RayFlagsCullFrontFacingTrianglesEXT;
    float tMin     = 0.001;
    float tMax     = 10000.0;

	// trace ray
	traceRayEXT(topLevelAs,     // acceleration structure
				rayFlags,       // rayFlags
				0xFF,           // cullMask
				0,              // sbtRecordOffset
				0,              // sbtRecordStride
				0,              // missIndex
				nextOrigin,     // ray origin
				tMin,           // ray min range
				nextDirection,  // ray direction
				tMax,           // ray max range
				0               // payload (location = 0)
	);
	
    prd.depth -= 1;
    
	// WIP: nans
    if(isnan(prd.radiance.x) || isnan(prd.radiance.y) || isnan(prd.radiance.z)){
    	prd.radiance = vec3(0,0,0);
    }
	return prd.radiance;
}

void main() {
	vec2 uv = (vec2(gl_LaunchIDEXT.xy) + vec2(0.5)) / vec2(gl_LaunchSizeEXT.xy);

	Surface surface = surfaceFromGBuffer(
	    cam,
		g_DepthSampler,
		g_NormalSampler,
		g_AlbedoSampler,
		g_SpecularSampler,
		g_EmissiveSampler,
		uv
		);
		
	
	if(surface.depth == 1.0) {
		imageStore(image, ivec2(gl_LaunchIDEXT.xy), vec4(0.0, 0.0, 0.0, 1.0));
		return; 
	}

	ReprojectionData reproj = GetReprojection(uv);
	float historyLength = reproj.historyLength;

	vec3 accumColor = vec3(0);
	uint actualSamples = 1;

	float historyCalculated = 1.;
	if (historyLength < 2) {
	   actualSamples = 1;
	   historyCalculated = 1.;
	}

	actualSamples *= samples;
	for(uint smpl = 0; smpl < samples; ++smpl){

		prd.seed = tea16(gl_LaunchIDEXT.y * gl_LaunchSizeEXT.x + gl_LaunchIDEXT.x, frame * samples + smpl);
		prd.depth = 1; // gbuffer

		vec3 brdf_NoL_invpdf = SampleSpecularDirection(surface, prd.seed);

		vec3 L = surfaceIncidentLightDir(surface);
		accumColor += RadianceOfRay(surface.position, L) * brdf_NoL_invpdf;
	}

	vec3 curColor = accumColor / float(actualSamples);
    

	vec2 moments;
	moments.r = luminance(curColor);
	moments.g = moments.r * moments.r;

	// Mix reprojection
	{
		#define minColorAlpha 0.05
		#define minMomentsAlpha 0.05

		const float colorAlpha = max(1. / (historyLength + 1.), minColorAlpha);
		const float momentsAlpha = max(1. / (historyLength + 1.), minMomentsAlpha);

		moments = mix(reproj.moments, moments, momentsAlpha);
		curColor = mix(reproj.color, curColor, colorAlpha);
	}

	// from SVGF paper:
	float variance = max(0., moments.g - moments.r * moments.r);

	variance *= 16.f / max(historyLength + historyCalculated, 32.f);

	if (historyLength < 10) {
		variance += 0.2;

	}

	imageStore(progressiveResult, ivec2(gl_LaunchIDEXT.xy), vec4(curColor, variance));
	imageStore(image, ivec2(gl_LaunchIDEXT.xy), vec4(curColor, variance));

	imageStore(momentsBuffer, ivec2(gl_LaunchIDEXT.xy), vec4(moments, surface.depth, historyLength + historyCalculated));
}

//
//  PRE PROCESS (reproject)
//

bool IsReprojValid(ivec2 coord, float expectedPrevDepth, vec4 centerUvDrawIndex, out vec4 momentsSample)
{
	if (coord.x < 0 || coord.y < 0 || coord.x >= gl_LaunchSizeEXT.x || coord.y > gl_LaunchSizeEXT.y) {
		return false;
	}

	momentsSample = imageLoad(momentsBuffer, coord);
	if (abs(expectedPrevDepth - momentsSample.z) > 5e-3) {
		return false;
	}

	

	vec4 thisUvDrawIndex = texelFetch(g_UVDrawIndexSampler, coord, 0);
	if (abs(thisUvDrawIndex.z - centerUvDrawIndex.z) > 0.5) {
		return false;
	}

	return true;
}


ReprojectionData ReprojSample(vec2 sampleUv, vec4 centerUvDrawIndex, float expectedPrevDepth)
{
	ivec2 pixelPos = ivec2(floor(sampleUv));
	ivec2 offset[4] = { ivec2(0, 0), ivec2(1, 0), ivec2(0, 1), ivec2(1, 1) };

	float x = fract(sampleUv.x);
	float y = fract(sampleUv.y);

	float w[4] = { 
		(1 - x) * (1 - y),
		      x * (1 - y),
		(1 - x) * y,
		      x * y 
	};

	vec4 color = vec4(0);
	vec4 moments = vec4(0);
	float sumW = 0;
	
	for (int sampleIdx = 0; sampleIdx < 4; sampleIdx++) {
		ivec2 loc = pixelPos + offset[sampleIdx];
		vec4 momentsSample;
		if (IsReprojValid(loc, expectedPrevDepth, centerUvDrawIndex, momentsSample)) {
			color += w[sampleIdx] * imageLoad(progressiveResult, loc);
			moments += w[sampleIdx] * momentsSample;
			sumW += w[sampleIdx];
		}
	}


	// This "valid" check should probably have min bias
	if (sumW > 0) {
		color /= sumW;
		moments /= sumW;
	}

	ReprojectionData reproj;
	reproj.color = color.rgb;
	reproj.moments = moments.rg;

	reproj.historyLength = moments.a;
	return reproj;
}

ReprojectionData GetReprojection(vec2 uv) {
	// PERF:
	if (frame < 1) {
		ReprojectionData repr;
		repr.color = vec3(0);
		repr.moments = vec2(0);
		repr.historyLength = 0;
		return repr;
	}

	// xy: ScreenSpace (uv) motion vector
	// z: expected previous depth
	const vec4 motionSample = texture(g_VelocitySampler, uv);

	const vec2 motion = motionSample.xy;
	const float expectedDepth = motionSample.z;

	const vec2 posPrev = vec2(gl_LaunchIDEXT.xy) - vec2(motion * gl_LaunchSizeEXT.xy);


	const vec4 centerUvDrawIndex = texelFetch(g_UVDrawIndexSampler, ivec2(gl_LaunchIDEXT.xy), 0);

	return ReprojSample(posPrev, centerUvDrawIndex, expectedDepth);
}
